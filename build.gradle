buildscript {
    ext.versions = [
            "kotlin": "1.5.21"
    ]
    ext.deps = [
            // Kotlin
            "kotlinStdLib"        : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",

            // Android
            "androidxCoreKtx"     : "androidx.core:core-ktx:1.6.0",
            "androidxAppcompat"   : "androidx.appcompat:appcompat:1.3.1",
            "material"            : "com.google.android.material:material:1.4.0",
            "constraintLayout"    : "androidx.constraintlayout:constraintlayout:2.1.0",

            // Testing
            "junit"               : "junit:junit:4.13.2",
            "androidxJUnit"       : "androidx.test.ext:junit:1.1.3",
            "androidxEspressoCore": "androidx.test.espresso:espresso-core:3.4.0"
    ]

    repositories {
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.dicedmelon.gradle:jacoco-android:0.1.5"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "${detektVersion}"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${detektVersion}"
}

detekt {
    toolVersion = detektVersion
    config = files("detekt/detekt-config.yml")
    input = files(
            "Core/src",
            "Card/src",
            "Demo/src"
    )
    autoCorrect = project.hasProperty('detektAutoCorrect')
    reports {
        html {
            enabled = true
            destination = file("build/reports/detekt_report.html")
        }
    }
}

subprojects {
    group = "com.paypal.android"
}
