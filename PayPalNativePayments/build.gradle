plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlinAndroid
}

android {
    namespace 'com.paypal.android.paypalnativepayments'
    java {
        toolchain {
            languageVersion = modules.kotlinToolchainLanguageVersion
        }
    }

    defaultConfig {
        // TODO: revert once Native XO supports minSdk 21
        //minSdkVersion 21
        minSdkVersion 23
        compileSdk modules.androidCompileSdk
        targetSdkVersion modules.androidTargetVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            version = android.defaultConfig.versionName
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility modules.sourceCompatibility
        targetCompatibility modules.targetCompatibility
    }
    kotlinOptions {
        jvmTarget = modules.kotlinJvmTarget
    }
    lintOptions {
        lintConfig file('../lint.xml')
        abortOnError true
        warningsAsErrors true
    }
}

dependencies {
    api project(':CorePayments')
    implementation project(':FraudProtection')

    implementation(libs.paypal.nativeCheckout) {
        exclude module: 'data-collector'
    }

    implementation libs.kotlin.stdLib
    implementation libs.androidx.coreKtx
    implementation libs.androidx.appcompat
    implementation libs.kotlinx.coroutinesAndroid
    implementation libs.android.material

    testImplementation libs.junit
    testImplementation libs.mockk
    testImplementation libs.robolectric
    testImplementation libs.kotlinx.coroutinesTest
    testImplementation libs.strikt.core
    testImplementation libs.strikt.mockk

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso
}

project.ext.name = "paypal-native-payments"
project.ext.version = rootProject.version
project.ext.pom_name = "PayPal SDK - Native Payments"
project.ext.pom_desc = "Library for PayPal native payments"

apply from: rootProject.file("gradle/gradle-publish.gradle")