plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlinAndroid
    alias libs.plugins.jacocoAndroid
    alias libs.plugins.kotlinParcelize
}

android {
    compileSdk modules.androidTargetVersion

    defaultConfig {
        minSdkVersion modules.androidMinSdkVersion
        targetSdkVersion modules.androidTargetVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField "String", "CLIENT_SDK_VERSION", "\"$versionName\""
    }

    buildTypes {
        release {
            version = android.defaultConfig.versionName // had to add this to get the version in the build
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    lintOptions {
        lintConfig file('../lint.xml')
        abortOnError true
        warningsAsErrors true
    }
    packagingOptions {
        // Ref: https://stackoverflow.com/a/44767574
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    // Ref: https://stackoverflow.com/a/37593189
    testOptions {
        unitTests {
            includeAndroidResources true
            all {
                // workaround for spying on java.net.URL in Http module
                // Ref: https://mockk.io/doc/md/jdk16-access-exceptions.html
                jvmArgs("--add-opens", "java.base/java.io=ALL-UNNAMED")
                jvmArgs("--add-opens", "java.base/java.net=ALL-UNNAMED")
            }
        }
    }

}

dependencies {
    implementation libs.androidx.coreKtx
    implementation libs.androidx.appcompat
    implementation libs.kotlin.stdLib
    implementation libs.kotlinx.coroutinesAndroid

    testImplementation libs.json
    testImplementation libs.jsonAssert
    testImplementation libs.junit
    testImplementation libs.mockk
    testImplementation libs.robolectric
    testImplementation libs.kotlinx.coroutinesTest
    testImplementation libs.androidx.test.core

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso
    androidTestImplementation libs.kotlinx.coroutinesTest
}

project.ext.name = "core-payments"
project.ext.version = rootProject.version
project.ext.pom_name = "PayPal SDK - CorePayments"
project.ext.pom_desc = "CorePayments library for PayPal payment methods"

apply from: rootProject.file("gradle/gradle-publish.gradle")
