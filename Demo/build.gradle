plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.github.triplet.play' version '3.8.4'
}

def paypalProperties = loadPropertiesFromFile("paypal.properties")

android {
    defaultConfig {
        applicationId "com.paypal.android"
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34
        versionCode modules.demoAppVersionCode
        versionName modules.sdkVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "CLIENT_ID", paypalProperties["CLIENT_ID"] ?: "\"\"")
    }

    buildFeatures {
        // Ref: https://developer.android.com/jetpack/compose/setup
        compose true
        viewBinding = true
    }

    composeOptions {
        // Ref: https://developer.android.com/jetpack/androidx/releases/compose-kotlin#pre-release_kotlin_compatibility
        kotlinCompilerExtensionVersion "${versions.compose}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        lintConfig file('../lint.xml')
        abortOnError true
        warningsAsErrors true
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/androidTestShared/java"
        }
    }

    // https://developer.android.com/build/build-variants#groovy
    signingConfigs {
        release {
            storeFile file(System.getenv('DEMO_KEYSTORE_FILE') ?: 'debug.keystore')
            storePassword System.getenv('DEMO_KEYSTORE_PASSWORD') ?: 'android'
            keyAlias System.getenv('DEMO_KEY_ALIAS') ?: 'androiddebugkey'
            keyPassword System.getenv('DEMO_KEY_PASSWORD') ?: 'android'
        }
    }
    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }
}

play {
    // Ref: https://github.com/Triple-T/gradle-play-publisher/issues/979#issuecomment-884763388
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
    serviceAccountCredentials.set(file(System.getenv("DEMO_GCP_SERVICE_ACCOUNT_CREDENTIALS_FILE") ?: '/path/to/credential_file.json'))
}

// Ref: https://developer.android.com/training/dependency-injection/hilt-android#setup
kapt {
    // allow references to generated code
    correctErrorTypes true
}

dependencies {
    debugImplementation project(':CardPayments')
    debugImplementation project(':FraudProtection')
    debugImplementation project(':PayPalNativePayments')
    debugImplementation project(':PayPalWebPayments')
    debugImplementation project(':PaymentButtons')
    debugImplementation project(':Venmo')

    releaseImplementation deps.cardPayments
    releaseImplementation deps.paymentButtons
    releaseImplementation deps.paypalWebPayments
    releaseImplementation deps.paypalNativePayments
    releaseImplementation deps.fraudProtection

    implementation libs.kotlin.std.lib
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.google.android.material
    implementation libs.androidx.constraint.layout
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.fragment.ktx

    // TODO: remove dependency when we don't relay on nxo models anymore
    implementation(deps.nativeCheckout) {
        exclude module: 'data-collector'
    }

    // Compose Bill of Materials (BOM) dependency manages compose dependency versions without
    // us having to explicitly state versions of individual compose dependencies; the BOM project
    // selects dependency versions for each compose dependency that are known by the JetPack team
    // to be compatible with one another. The set of dependencies output by the bom have been tested
    // and are known to be compatible by the Jetpack Team
    // More info: https://developer.android.com/jetpack/compose/bom
    // Another ref: https://developer.android.com/jetpack/compose/setup#setup-compose
    implementation platform(libs.androidx.compose.bom)
    androidTestImplementation platform(libs.androidx.compose.bom)

    implementation libs.androidx.compose.material3
    implementation libs.androidx.compose.ui.tooling.preview
    debugImplementation libs.androidx.compose.ui.tooling

    implementation libs.androidx.lifecycle.view.model.compose
    implementation libs.androidx.lifecycle.runtime.compose

    implementation libs.androidx.preference.ktx

    implementation libs.square.retrofit2
    implementation libs.square.converter.gson
    implementation libs.square.logging.interceptor

    implementation deps.hilt
    kapt deps.hiltKapt

    implementation libs.androidx.navigation.compose
    implementation libs.hilt.navigation.compose

    testImplementation deps.junit

    androidTestImplementation deps.androidxJUnit
    androidTestImplementation deps.androidxEspressoCore
    androidTestImplementation deps.androidxTestRunner
    androidTestImplementation deps.androidxTestRules
    androidTestImplementation deps.androidxTestUiAutomator

    // compose ui-test
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.test.manifest
}

/**
 * Loads properties at the specified file path. If the file does not exist,
 * this method returns an empty properties object.
 * @param filePath
 */
def loadPropertiesFromFile(filePath) {
    def result = new Properties()
    try {
        result.load(new FileInputStream(rootProject.file(filePath)))
    } catch (e) { /* ignored */
    }
    return result
}
